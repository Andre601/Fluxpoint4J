plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'maven-publish'
}

group 'ch.andre601'
version '1.2.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    api group: 'org.jetbrains', name: 'annotations', version: '18.0.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.3'
    
    testImplementation group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.32'
    testImplementation group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.32'
    
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.2'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.2'
}

test {
    useJUnitPlatform()
}

configurations {
    testIplementation {
        extendsFrom(compileOnly)
    }
}

javadoc {
    source = sourceSets.main.allJava
    destinationDir = file("$rootDir/docs/")
    
    options.encoding("UTF-8")
    options.setMemberLevel(JavadocMemberLevel.PUBLIC)
    
    exclude(
            "ch/andre601/fluxpoint4j/request/RequestHandler.java",
            "ch/andre601/fluxpoint4j/util/ColorObjectSerializer.java",
            "ch/andre601/fluxpoint4j/CheckUtil.java"
    )
    
    if(options instanceof StandardJavadocDocletOptions) {
        def opt = options as StandardJavadocDocletOptions
        
        opt.links(
                "https://docs.oracle.com/javase/8/docs/api"
        )
        
        if(JavaVersion.current().isJava9Compatible()) {
            opt.addBooleanOption("html5", true)
            opt.addStringOption("-release", "8")
        }
        if(JavaVersion.current().isJava11Compatible()) {
            opt.addBooleanOption("-no-module-directories", true)
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}


